plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '2.0.2'
}

defaultTasks 'build', 'shadowJar'

group = 'com.nguyenquyhy'
version = '3.0.0'
description = 'A plugin to connect Discord and Minecraft'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        name = 'Sonatype'
        url = 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        name = 'sponge'
        url = 'http://repo.spongepowered.org/maven'
    }
    maven {
        name = 'jitpack.io'
        url = 'https://jitpack.io'
    }
    maven {
        name = "Nucleus"
        url = "http://repo.drnaylor.co.uk/artifactory/list/minecraft"
    }
    jcenter()
}

dependencies {
    compile 'org.spongepowered:spongeapi:7.+'
    compile 'net.dv8tion:JDA:3.5.1_339'
    compile 'com.mashape.unirest:unirest-java:1.4.9'
    compile 'io.github.nucleuspowered:nucleus-api:+'
}

task fatJar(type: Jar) {
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

shadowJar {
    dependencies {
        include dependency('commons-logging:commons-logging')
        include dependency('org.apache.commons:commons-lang3')
        include dependency('org.apache.httpcomponents:httpcore')
        include dependency('org.apache.httpcomponents:httpclient')

        relocate 'org.apache.http', 'shaded.apache.http'
        relocate 'org.apache.commons', 'shaded.apache.commons'
    }
}

build.dependsOn(shadowJar)
